<!DOCTYPE html>
<html>
	<head>
		<title>Epiphyte Corp.</title>
		
		<meta name="apple-mobile-web-app-capable" content="yes" />
		<meta name="apple-mobile-web-app-status-bar-style" content="black-translucent" />
		<meta name="viewport" content="width = device-width, initial-scale = 1, user-scalable = no" />
		<link rel="apple-touch-startup-image" href="images/splash.png">

		<script src="http://cdnjs.cloudflare.com/ajax/libs/coffee-script/1.7.1/coffee-script.min.js"></script>
		<script src="http://ajax.googleapis.com/ajax/libs/jquery/2.1.1/jquery.min.js"></script>
		<script src="http://cdnjs.cloudflare.com/ajax/libs/howler/1.1.17/howler.min.js"></script>
		<script src="http://cdnjs.cloudflare.com/ajax/libs/underscore.js/1.7.0/underscore-min.js"></script>
		<script src="http://cdnjs.cloudflare.com/ajax/libs/backbone.js/1.1.2/backbone-min.js"></script>
		<script src="http://cdnjs.cloudflare.com/ajax/libs/backbone.marionette/2.2.2/backbone.marionette.min.js"></script>
		
		<link rel="stylesheet" href="css/style.css">

	</head>
	<body>

		<script src="./assets.js"></script>
		<script type="text/coffeescript">


			class Sound extends Backbone.Model
				initialize: ({urls, volume, sprite, repeatDelay}) ->
					@set 'clip', new Howl
						urls: urls
						sprite: sprite
						onend: @_handleOnEnd
						volume: volume
				stop: ->
					@get( 'clip' ).stop()
					this

				play: (sprite) ->
					@get( 'clip' ).play( sprite )
					this

				then: (cb) ->
					@onEndCallback = cb
					this

				_handleOnEnd: =>
					console.log "handle onend"
					@onEndCallback?()
					if @get 'repeatDelay'
						setTimeout ( =>
							console.log "repeating sound", this
							@play()

						), @repeatDelay

			class State extends Backbone.Model
				initialize: ({url, text}) ->
					@_createSound( url )
					
				_createSound: (url) =>
					@set 'sound', new Sound
						urls: [url]
						volume: 1


			class StateCollection extends Backbone.Collection
				initialize: (options)->

				model: State
				getStart: ->
					@findWhere( id: 'start' )
				getState: (state) ->
					@findWhere( id: state )

			class Player extends Backbone.Model
				defaults:
					state: null
					states: null
				initialize: ({@states}) ->						
					@set( 'state', null )
					@on 'change:state', @_handleChangeState
					@set( 'state', @states.getStart() )

				play: ->
					state = @get( 'state' )
					console.log "playing state: ", state.get( 'id' )
					console.log "available actions: ", state.get( 'actions' )
					state.get( 'sound' )?.play()

				stop: ->
					@get( 'state' )?.get( 'sound' )?.stop()

				setState: (state) ->
					if @states.getState( state )
						@stop()
						@set( 'state', @states.getState( state ) )
					else
						@set( 'state', @states.getState( 'nosuchstateasset' ) )
					@play()

				_handleChangeState: ->
					console.log "handle change state?", arguments

			class InputController extends Backbone.Model
				initialize: ({ @player }) ->
				handleInput: (value) ->
					state = @player.get( 'state' )
					actions = state.get( 'actions' )
					@player.stop()
					@player.setState( actions[value] )
					


			class IndicatorView extends Marionette.ItemView
				tagName: 'ol'
				className: 'indicator-view'
				template: _.template """
					<li class="message">
						<div class="light"></div>
						<div class="label">Message</div>
					</li>
					<li class="func">
						<div class="light"></div>
						<div class="label">Func</div>
					</li>
				"""
				_toggle: (cls, force) =>
					@$( cls ).toggleClass( 'on', force )

				toggleMessage: (force) -> @_toggle( '.message', force )
				toggleFunc: (force) -> @_toggle( '.func', force )



			class AppView extends Marionette.LayoutView
				className: 'app-view'
				template: _.template """
					<div class="indicator-region"></div>
					<div class="numpad-region"></div>
					<p class="reg">&copy; 452 Corp. RT023</p>
				"""
				regions:
					indicatorRegion: '.indicator-region'
					numpadRegion: '.numpad-region'

				initialize: ({ states }) ->
					@numpadView = new NumpadView()
					@indicatorView = new IndicatorView()
					@player = new Player
						states: states

					@inputController = new InputController( player: @player )

					@listenTo @numpadView, 'end', =>
						console.log "hangup"
						@player.setState( '' )

					@listenTo @numpadView, 'press', (value) =>
						console.log "pressed #{value}"
						@inputController.handleInput( value )
					
					@listenTo @numpadView, 'done:ring', =>
						console.log "done ringing"
						@player.setState( 'start' )

				onShow: ->
					@numpadRegion.show( @numpadView )
					@indicatorRegion.show( @indicatorView )
				
				onClose: ->
					@player.stop()



			class NumpadView extends Marionette.ItemView
				tagName: 'ol'
				className: 'numpad-view'

				initialize: ->

					@longBeepSound = new Sound
						urls: ['sfx/beep.mp3']
						volume: 1

					@beepSound = new Sound
						urls: ['sfx/beep2.mp3']
						sprite:
							short: [0, 100]
							medium: [0, 1500]
							long: [0, 3000]
						volume: 1

					@ringSound = new Sound
						urls: ['sfx/ringing.mp3']
						volume: 1
						sprite:
							short: [0, 5000]
							medium: [0, 10000]
							long: [0, 30000]
						
					@clickSound = new Sound
						urls: ['sfx/click.mp3']
						volume: 0.5

				events:
					'click li': '_handlePressButton'
					'click li.hang-up': '_handleHangUp'

				_handleHangUp: ->
					@running = false
					window.location.reload()

				_startCall: =>
					console.log "startcall"
					@longBeepSound.play().then =>
						console.log "ring"
						@ringSound.play( 'short' ).then =>
							@running = true
							@trigger( 'done:ring' )

				_handlePressButton: (ev) ->
					@beepSound.stop()
					@ringSound.stop()

					console.log @clickSound.play()
					@beepSound.play( 'short' )

					$item = $( ev.target )
					$item.addClass( 'active' )
					setTimeout ( -> $item.removeClass( 'active' ) ), 100
					val = $item.find('.number').html()
					if val is 'R'
						@_startCall()
					else
						@trigger(  "press", val ) if @running

				onShow: ->

				template: _.template """
					<li>
						<a href="#">
							<span class="alpha"></span>
							<span class="number">1</span>
						</a>
					</li>
					<li>
						<a href="#">
							<span class="alpha">ABC</span>
							<span class="number">2</span>
						</a>
					</li>
					<li>
						<a href="#">
							<span class="alpha">DEF</span>
							<span class="number">3</span>
						</a>
					</li>
					<li>
						<a href="#">
							<span class="alpha">GHI</span>
							<span class="number">4</span>
						</a>
					</li>
					<li>
						<a href="#">
							<span class="alpha">JFK</span>
							<span class="number">5</span>
						</a>
					</li>
					<li>
						<a href="#">
							<span class="alpha">MNO</span>
							<span class="number">6</span>
						</a>
					</li>
					<li>
						<a href="#">
							<span class="alpha">PQRS</span>
							<span class="number">7</span>
						</a>
					</li>
					<li>
						<a href="#">
							<span class="alpha">TUV</span>
							<span class="number">8</span>
						</a>
					</li>
					<li>
						<a href="#">
							<span class="alpha">WXYZ</span>
							<span class="number">9</span>
						</a>
					</li>
					<li>
						<a href="#">
							<span class="alpha"></span>
							<span class="number">*</span>
						</a>
					</li>
					<li>
						<a href="#">
							<span class="alpha">OPER</span>
							<span class="number">0</span>
						</a>
					</li>
					<li>
						<a href="#">
							<span class="alpha"></span>
							<span class="number">#</span>
						</a>
					</li>
					<li class="mem special">
						<a href="#">
							<span class="alpha">MEM</span>
							<span class="number">R</span>
						</a>
					</li>
					<li class="hang-up special">
						<a href="#">
							<span>Hang Up</span>
						</a>
					</li>
				"""

			

			states = new StateCollection( _.values( window.RT023_ASSETS ) )

			appRegion = new Marionette.Region( el: $('body')[0] )
			window.appView = appView = new AppView( states: states )

			window.RT023_term_override =
				setState: (state) -> appView.player.setState( state )
				getStates: -> states

			$ -> appRegion.show( appView )

			

			

		</script>
		
	</body>
</html>