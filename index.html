<!DOCTYPE html>
<html>
	<head>
		<title>Epiphyte Corp.</title>
		
		<meta name="apple-mobile-web-app-capable" content="yes" />
		<meta name="apple-mobile-web-app-status-bar-style" content="black-translucent" />
		<meta name="viewport" content="width = device-width, initial-scale = 1, user-scalable = no" />

		<script src="http://cdnjs.cloudflare.com/ajax/libs/coffee-script/1.7.1/coffee-script.min.js"></script>
		<script src="http://ajax.googleapis.com/ajax/libs/jquery/2.1.1/jquery.min.js"></script>
		<script src="http://cdnjs.cloudflare.com/ajax/libs/howler/1.1.17/howler.min.js"></script>
		<script src="http://cdnjs.cloudflare.com/ajax/libs/underscore.js/1.7.0/underscore-min.js"></script>
		<script src="http://cdnjs.cloudflare.com/ajax/libs/backbone.js/1.1.2/backbone-min.js"></script>
		<script src="http://cdnjs.cloudflare.com/ajax/libs/backbone.marionette/2.2.2/backbone.marionette.min.js"></script>
		
		<link rel="stylesheet" href="css/style.css">

	</head>
	<body>


		<script type="text/coffeescript">

			class Sound extends Backbone.Model
				initialize: ({urls, volume, sprite, repeatDelay}) ->
					@set 'clip', new Howl
						urls: urls
						sprite: sprite
						onend: @_handleOnEnd
						volume: volume
				stop: ->
					@get( 'clip' ).stop()
					this

				play: (sprite) ->
					@get( 'clip' ).play( sprite )
					this

				then: (cb) ->
					@onEndCallback = cb
					this

				_handleOnEnd: =>
					@onEndCallback?()
					if @get 'repeatDelay'
						setTimeout ( =>
							console.log "repeating sound", this
							@play()

						), @repeatDelay


			class State extends Backbone.Model
				initialize: ({ @url, @repeatDelay, @actions }) ->

			class Player extends Backbone.Model
				initialize: ({state, @states}) ->
					if state
						@state = new State( @states[state] )
						return

					throw new Error('No state provided to Player!')

				play: ->
					@_createSound() unless @sound
					console.log "playing state: ", @state
					console.log "available actions: ", @state.actions
					@sound.play()

				stop: ->
					@sound?.stop()

				setState: (state) ->
					@sound?.stop()
					console.log "changing state to: #{ state }.", @states
					@state = new State( @states[state] )
					@_createSound()
					@play()
						
				_createSound: =>
					@sound = new Sound
						urls: [@state.url]
						volume: 1

			class InputController extends Backbone.Model
				initialize: ({ @player }) ->
				handleInput: (value) ->
					actions = @player.state.actions
					@player.stop()
					@player.setState( actions[value] ) if actions?[value]


			class IndicatorView extends Marionette.ItemView
				tagName: 'div'
				className: 'indicator-view'
				template: _.template ""


			class AppView extends Marionette.LayoutView
				className: 'app-view'
				template: _.template """
					<div class="indicator-region"></div>
					<div class="numpad-region"></div>
				"""
				regions:
					indicatorRegion: '.indicator-region'
					numpadRegion: '.numpad-region'

				initialize: ->
					@numpadView = new NumpadView()
					@indicatorView = new IndicatorView()
					@player = new Player
						state: 'start'
						states:
							start:
								url:'assets/start.mp3'
								repeatDelay: 5000
								actions:
									"1": 'general'
									"2": 'start'
									"2": 'options'
							hold:
								url:'assets/hold.mp3'
								repeatDelay: 5000

							yournumber:
								url:'assets/yournumber.mp3'
								repeatDelay: 5000
								actions:
									"9": 'start'
							general:
								url:'assets/general.mp3'
								repeatDelay: 5000
								actions:
									"1": 'hold'
									"2": 'yournumber'
									"9": 'hold'
							options:
								url:'assets/options.mp3'
								repeatDelay: 5000
								actions:
									"1": 'options'
									"9": 'start'
							recent:
								url:'assets/recent.mp3'
								repeatDelay: 5000
								actions:
									"1": 'hold'
									"2": 'start'

					@inputController = new InputController( player: @player )

					@listenTo @numpadView, 'end', =>
						console.log "hangup"
						@player.setState( '' )

					@listenTo @numpadView, 'press', (value) =>
						console.log "pressed #{value}"
						@inputController.handleInput( value )
					
					@listenTo @numpadView, 'done:ring', =>
						console.log "done ringing"
						@player.setState( 'start' )

				onShow: ->
					@numpadRegion.show( @numpadView )
					@indicatorRegion.show( @indicatorView )
				
				onClose: ->
					@player.stop()



			class NumpadView extends Marionette.ItemView
				tagName: 'ol'
				className: 'numpad-view'

				initialize: ->

					@longBeepSound = new Sound
						urls: ['sfx/beep.mp3']
						volume: 1

					@beepSound = new Sound
						urls: ['sfx/beep2.mp3']
						sprite:
							short: [0, 100]
							medium: [0, 1500]
							long: [0, 3000]
						volume: 1

					@ringSound = new Sound
						urls: ['sfx/ringing.mp3']
						volume: 1
						sprite:
							short: [0, 5000]
							medium: [0, 10000]
							long: [0, 30000]
						
					@clickSound = new Sound
						urls: ['sfx/click.mp3']
						volume: 1

				events:
					'click li': '_handlePressButton'
					'click li.hang-up': '_handleHangUp'

				_handleHangUp: ->
					window.location.reload()

				_startCall: =>
					console.log "startcall"
					@longBeepSound.play().then =>
						console.log "ring"
						@ringSound.play( 'short' ).then =>
							@running = true
							@trigger( 'done:ring' )

				_handlePressButton: (ev) ->
					@beepSound.stop()
					@ringSound.stop()

					console.log @clickSound.play()
					@beepSound.play( 'short' )

					$item = $( ev.target )
					val = $item.find('.number').html()
					if val is 'R'
						@_startCall()
					else
						@trigger(  "press", val ) if @running

				onShow: ->

				template: _.template """
					<li>
						<a href="#">
							<span class="alpha"></span>
							<span class="number">1</span>
						</a>
					</li>
					<li>
						<a href="#">
							<span class="alpha">ABC</span>
							<span class="number">2</span>
						</a>
					</li>
					<li>
						<a href="#">
							<span class="alpha">DEF</span>
							<span class="number">3</span>
						</a>
					</li>
					<li>
						<a href="#">
							<span class="alpha">GHI</span>
							<span class="number">4</span>
						</a>
					</li>
					<li>
						<a href="#">
							<span class="alpha">JFK</span>
							<span class="number">5</span>
						</a>
					</li>
					<li>
						<a href="#">
							<span class="alpha">MNO</span>
							<span class="number">6</span>
						</a>
					</li>
					<li>
						<a href="#">
							<span class="alpha">PQRS</span>
							<span class="number">7</span>
						</a>
					</li>
					<li>
						<a href="#">
							<span class="alpha">TUV</span>
							<span class="number">8</span>
						</a>
					</li>
					<li>
						<a href="#">
							<span class="alpha">WXYZ</span>
							<span class="number">9</span>
						</a>
					</li>
					<li>
						<a href="#">
							<span class="alpha"></span>
							<span class="number">*</span>
						</a>
					</li>
					<li>
						<a href="#">
							<span class="alpha">+</span>
							<span class="number">0</span>
						</a>
					</li>
					<li>
						<a href="#">
							<span class="alpha"></span>
							<span class="number">#</span>
						</a>
					</li>
					<li class="mem special">
						<a href="#">
							<span class="alpha">MEM</span>
							<span class="number">R</span>
						</a>
					</li>
					<li class="hang-up special">
						<a href="#">
							<span>Hang Up</span>
						</a>
					</li>
				"""


			appRegion = new Marionette.Region( el: $('body')[0] )
			appView = new AppView()

			$ -> appRegion.show( appView )

			

			

		</script>
		
	</body>
</html>